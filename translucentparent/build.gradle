apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 35
    namespace 'com.cy.translucentparent'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //指定jdk版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'androidx.appcompat:appcompat:1.7.0'
    compileOnly 'androidx.activity:activity:1.8.0'
}

//---------------------------------------------下面的必加,加上下面的作用是在上传后会带上注释,不然会发现没有注释
// 打包源码
afterEvaluate {
    // 生成 javadoc
    task androidJavadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        failOnError false
    }

    // javadoc.jar
    task javadocJar(type: Jar) {
        dependsOn androidJavadoc
        archiveClassifier.set("javadoc")
        from androidJavadoc.destinationDir
    }

    // sources.jar
    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from android.sourceSets.main.java.srcDirs
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.release
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
